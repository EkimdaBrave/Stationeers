# Advanced airlock for interiors or atmo
# Now with door buttons and good code
# Stolen by: Ekimdabrave
# 2021-12-24 22:57:57

#Change these as necessary
define TargetInteriorPressure 20
define TargetExteriorPressure 20
#That's it, you don't have to read any more
alias DoorIn d0
alias DoorEx d1
alias GasSensor d2
alias VentIn d3
alias VentEx d4
alias VentIn2 d5 #optional

# Unlock these if you really want
s VentIn Lock 1
s VentEx Lock 1
brdns d5 2
s VentIn2 Lock 1

s DoorIn Mode 1
s DoorEx Mode 1
s DoorIn Lock 0
s DoorEx Lock 0 #cant lock in logic mode
alias State r0
alias Activated r1
alias pressure r2
alias temp r12
alias temp2 r13

start:
s db Setting State #for debug
l pressure GasSensor Pressure
#Check for activation
move Activated 0 #Activated = 0
l temp DoorIn Open #temp = DoorIn.Open
l temp2 DoorIn Setting #temp2 = DoorIn.Setting

bneal temp temp2 activated#if(temp!=temp2)activated
s DoorIn Setting temp #Prevents double-fires
l temp DoorEx Open # temp = DoorEx.Open
l temp2 DoorEx Setting #temp2 = DoorEx.Setting

bneal temp temp2 activated#if(temp!=temp2)activated
s DoorEx Setting temp#DoorEx.Setting = DoorEx.Open
beq State 0 state0#if State == 0 goto state0
beq State 1 state1#etc
beq State 2 state2
beq State 3 state3
beq State 4 state4
beq State 5 state5
# Jump to our state
state0: #exterior is open
beq Activated 0 end #if(Activated==0)goto end
s DoorEx Open 0
s DoorIn Open 0
s VentEx Mode 1 #Inward
s VentEx On 1
j nextState
state1: #depressurizing to external
breq Activated 1 3
brle pressure 0 2
j end
#If activated or pressure<=0
s VentEx On 0
s VentIn Mode 0 #outward
s VentIn On 1
brdns d5 3
s VentIn2 Mode 0
s VentIn2 On 1
j nextState
state2: #pressurizing to internal
breq Activated 1 3
brge pressure TargetInteriorPressure 2
j end
s VentIn On 0
brdns d5 2
s VentIn2 On 0
s DoorIn Open 1
j nextState
state3: #interior is open
beq Activated 0 end
s DoorEx Open 0
s DoorIn Open 0
s VentIn Mode 1 #inward
s VentIn On 1
brdns d5 3
s VentIn2 Mode 1
s VentIn2 On 1
j nextState
state4: #depressurizing to internal
breq Activated 1 3
brle pressure 0 2
j end
s VentIn On 0
brdns d5 2
s VentIn2 On 0
s VentEx Mode 0 #outward
s VentEx On 1
j nextState
state5: #pressurizing to external
breq Activated 1 3
brge pressure TargetExteriorPressure 2
j end
s VentEx On 0
s DoorEx Open 1
j nextState

activated:
move Activated 1#Activated=1
j ra#goto ra

nextState:
add State State 1 #State++
ble State 5 end #If State <= 5 goto end
move State 0 #State = 0
end: #nextState falls into end
yield #Just to yield
j start
